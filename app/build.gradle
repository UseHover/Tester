buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.usehover.com/snapshots' }
    maven { url 'https://maven.usehover.com/releases' }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.hover.tester"
        minSdkVersion 18
        maxSdkVersion 28
        targetSdkVersion 28
        versionCode 12
        versionName "0.8.7"
        vectorDrawables.useSupportLibrary true
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "one"
    productFlavors {
        gateway {
            dimension "one"
            applicationIdSuffix ".gateway"
            versionNameSuffix "-gateway"

        }
        manual {
            dimension "one"
            applicationIdSuffix ".manual"
            versionNameSuffix "-manual"
        }
        demo {
            dimension "one"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') { transitive = true }
    gatewayImplementation 'com.google.firebase:firebase-core:16.0.7'
    gatewayImplementation 'com.google.firebase:firebase-messaging:17.3.4'

    implementation 'com.android.volley:volley:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0-beta01'

//    gatewayImplementation('com.hover:android-sdk:0.14.7-nopin')
    releaseImplementation('com.hover:android-sdk:1.2.3-androidx-SNAPSHOT') { transitive = true }

    debugImplementation project(":hover.sdk")
    debugImplementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    debugImplementation "android.arch.work:work-runtime:1.0.1"
    debugImplementation 'com.google.android.gms:play-services-analytics:16.0.7'
    debugImplementation 'io.sentry:sentry-android:1.7.21'

    testImplementation 'junit:junit:4.12'
}

// https://docs.gradle.org/current/userguide/customizing_dependency_resolution_behavior.html#sec:dependency_substitution_rules
configurations {
    debug {
        resolutionStrategy.dependencySubstitution {
            substitute module("com.hover:android-sdk") with project(":hover.sdk")
        }
    }
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Gateway")) {
    apply plugin: 'com.google.gms.google-services'
}
